// SPDX-FileCopyrightText: the secureCodeBox authors
//
// SPDX-License-Identifier: Apache-2.0

plugins {
  id "java"
  id "io.freefair.lombok" version "9.1.0"
  // https://github.com/ben-manes/gradle-versions-plugin
  // Run: ./gradlew dependencyUpdates -Drevision=release
  id "com.github.ben-manes.versions" version "0.53.0"
  id "org.sonarqube" version "7.2.2.6593"
}

group = "io.securecodebox"
version = "1.0.0-SNAPSHOT"

repositories {
  mavenCentral()
  maven {
    url = "https://oss.sonatype.org/content/repositories/snapshots/"
  }
}

dependencies {
  implementation group: "io.securecodebox", name: "defectdojo-client", version: "2.0.1"
  implementation group: "io.kubernetes", name: "client-java", version: "20.0.1"
   // will not be updated to 7.0.0 because it no longer implements a class
   // so it causes issues with the version in the defectdojo client
  implementation group: "org.springframework", name: "spring-web", version: "6.2.12"
  // https://github.com/FasterXML/jackson-bom
  implementation platform("com.fasterxml.jackson:jackson-bom:2.20.1")
  implementation "com.fasterxml.jackson.core:jackson-core"
  implementation "com.fasterxml.jackson.core:jackson-annotations"
  implementation "com.fasterxml.jackson.core:jackson-databind"
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
  implementation group: "org.slf4j", name: "slf4j-api", version: "2.0.17"
  implementation group: "org.slf4j", name: "slf4j-log4j12", version: "2.0.17"
  // If I try to notate this like the others (with separate strings) I got errors. No idea why sh... Gradle
  // want it like this. It is the official documented example:
  // https://github.com/junit-team/junit5-samples/blob/r5.10.0/junit5-jupiter-starter-gradle/build.gradle
  testImplementation(platform("org.junit:junit-bom:6.0.2"))
  testImplementation("org.junit.jupiter:junit-jupiter")
  testImplementation group: "org.mockito", name: "mockito-core", version: "5.21.0"
  testImplementation group: "org.mockito", name: "mockito-junit-jupiter", version: "5.21.0"
  testImplementation group: 'org.hamcrest', name: 'java-hamcrest', version: '2.0.0.0'
  testImplementation group: 'uk.org.webcompere', name: 'system-stubs-jupiter', version: '2.1.8'
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
}

test {
  useJUnitPlatform()

  testLogging {
    events "failed"
    exceptionFormat = "full"
  }
}

def mainClassName = "io.securecodebox.persistence.DefectDojoPersistenceProvider"

jar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  manifest {
    attributes "Main-Class": "$mainClassName"
  }

  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }

  // Exclude signatures from dependency jars, as these would not match the fat jars content.
  exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
}

sonar {
  properties {
    property "sonar.projectKey", "isecureCodeBox_defectdojo-persistence-provider"
    property "sonar.organization", "iosecurecodebox"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}
